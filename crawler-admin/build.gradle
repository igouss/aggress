plugins {
    id "java"
    id "idea"
    id "com.github.ben-manes.versions" version "0.13.0"// gradle dependencyUpdates
}

ext {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    compile "ch.qos.logback:logback-core:1.1.7"
    compile "ch.qos.logback:logback-classic:1.1.7"

    compile "junit:junit:4.12"

    compile "com.vividsolutions:jts:1.13"
    compile "org.thymeleaf:thymeleaf:3.0.2.RELEASE"
    compile "io.vertx:vertx-core:3.3.3"
    compile "io.vertx:vertx-web:3.3.3"

    compile project(':crawler')
    compile project(':common')
}

task copyDepsToDocker(type: Copy) {
    description = "Copy all dependencies to docker/crawler-admin"
    from configurations.compile
    into "../docker/crawler-admin/deps"
}

task copyAppToDocker(type: Copy) {
    description = "Copy app to docker/crawler-admin"
    dependsOn jar
    from("$buildDir/libs") {
        include "crawler-admin*.jar"
    }
    into("../docker/crawler-admin/app")
}

task copyBaseDir(type: Copy) {
    description = "Copy basedir to docker/crawler-admin/basedir"
    from "basedir"
    into "../docker/crawler-admin/basedir"
}

task dockerPublish() {
    description = "Copy app and all dependencies to docker/crawler-admin"
    dependsOn copyDepsToDocker, copyAppToDocker, copyBaseDir
}