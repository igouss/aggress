buildscript {
    ext.kotlin_version = kotlinVersion
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}


dependencies {
    compile "ch.qos.logback:logback-core:1.2.3"
    compile "ch.qos.logback:logback-classic:1.2.3"

    compile "junit:junit:4.12"

    compile("org.elasticsearch.client:transport:$elasticTransportVersion") {
        exclude group: 'org.elasticsearch.plugin', module: 'transport-netty3-client'
    }

    compile 'org.apache.logging.log4j:log4j-to-slf4j:2.8.2'

    compile "com.vividsolutions:jts:1.13"

    compile "org.thymeleaf:thymeleaf:3.0.6.RELEASE"

    compile "io.vertx:vertx-core:3.4.1"
    compile "io.vertx:vertx-web:3.4.1"

    compile project(':common')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

task copyDepsToDocker(type: Copy) {
    description = "Copy all dependencies to docker/frontend"
    from configurations.compile
    into "../docker/frontend/deps"
}

task copyAppToDocker(type: Copy) {
    description = "Copy app to docker/frontend"
    dependsOn jar
    from("$buildDir/libs") {
        include "frontend*.jar"
    }
    into("../docker/frontend/app")
}

task copyBaseDir(type: Copy) {
    description = "Copy basedir to docker/frontend/basedir"
    from "basedir"
    into "../docker/frontend/basedir"
}

task dockerPublish() {
    description = "Copy app and all dependencies to docker/frontend"
    dependsOn copyDepsToDocker, copyAppToDocker, copyBaseDir
}

repositories {
    mavenCentral()
}